;************************************************************************
; String comparision subprogram that compares two strings (Sobjects) and
; returns a number less than zero if the first object is lexiographicall
; before (i.e. less than) the second, zero if they are equivalent, and a
; positive number otherwise.
;
;********  int ScompTo (char[] Sobject1, char[] Sobject2)
;
;-- Arguments -----------------------------------------------------------
Sobject1:  .EQUATE   2             ;formal parameter; address of Sobject
Sobject2:  .EQUATE   4             ;formal parameter; address of Sobject
;-- Local Data ----------------------------------------------------------
hold2:     .BLOCK    2
COMMA:     .ASCII    ",\x00"
NL:        .ASCII    "\n\x00"
;-- Entry Point ---------------------------------------------------------
           .GLOBAL   ScompTo
ScompTo:   SAVEX
           CLRWA                   ;  A = 0;
           CLRWX                   ;  X = 0;
loop:      NOP0                    ;+ loop {
           LDBA      Sobject2,sfx  ;| + if(Sobject2[X] == 0x00) {
           TSTWA                   ;| |   EXIT pos;
           BREQ      pos           ;| + }
           STWA      hold2,d       ;|   hold2 = Sobject2[X];
           LDBA      Sobject1,sfx  ;| + if(Sobject1[X] == 0x00) {
           TSTWA                   ;| |   EXIT makeneg;
           BREQ      makeneg       ;| + }
           CPWA      hold2,d       ;| + if(Sobject1[X] ? hold2) {
           BRLT      makeneg       ;| |   WHEN < EXIT makeneg;
           BRGT      makepos       ;| |   WHEN > EXIT makepos;
           NOP0                    ;| + }
           INCX                    ;|   X = X + 1;
           BR        loop          ;+ }
;---------
makeneg:   LDWA      -1,i
           BR        done
;---------
pos:       LDBA      Sobject1,sfx
           TSTWA
           BREQ      makezero
makepos:   LDWA      1,i
           BR        done
;---------
makezero:  CLRWA
;---------
done:      RESTOREX
           RET                     ;
